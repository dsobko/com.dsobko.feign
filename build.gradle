buildscript {
    ext {
        kotlinVersion = '1.2.0'
        feignVersion = '9.4.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: "kotlin-allopen"
apply plugin: 'idea'
apply plugin: 'application'

configurations {
    ktlint
}



jar {
    baseName = 'feign-functional-tests'
    version = '1.0.0-SNAPSHOT'
}
sourceCompatibility = 1.8

mainClassName = 'com.feign.test.PhoneClientLiveTest'

repositories {
    mavenCentral()
}

dependencies {
    compile "io.github.openfeign:feign-core:${feignVersion}"
    compile "io.github.openfeign:feign-okhttp:${feignVersion}"
    compile "io.github.openfeign:feign-gson:${feignVersion}"
    compile "io.github.openfeign:feign-slf4j:${feignVersion}"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    ktlint 'com.github.shyiko:ktlint:0.8.1'
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

task ktlint(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec) {
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


